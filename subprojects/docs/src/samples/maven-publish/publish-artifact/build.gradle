plugins {
    id 'base'
    id 'maven-publish'
}

group = 'org.gradle.sample'
version = '1.0'

// START SNIPPET custom-artifact
def rpmFile = file("$buildDir/rpms/my-package.rpm")
def rpmArtifact = artifacts.add('archives', rpmFile) {
    type 'rpm'
    builtBy 'rpm'
}
// END SNIPPET custom-artifact

task rpm() {
    outputs.file rpmFile
    doLast {
        // produce real RPM here
        rpmFile << 'file contents'
    }
}

// START SNIPPET custom-artifact-publication
publishing {
    publications {
        maven(MavenPublication) {
            artifact rpmArtifact
        }
    }
// END SNIPPET custom-artifact-publication
    repositories {
        // change URLs to point to your repo, e.g. http://my.org/repo
        maven {
            url = "$buildDir/repo"
        }
    }
// START SNIPPET custom-artifact-publication
}
// END SNIPPET custom-artifact-publication
